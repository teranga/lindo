buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
    }

    dependencies {
        classpath "com.github.adrianbk:gradle-jvmsrc-plugin:0.6.1"
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.0'
    }
}
apply plugin: 'download-task'
apply plugin: 'spring-boot'
apply plugin: 'com.github.adrianbk.jvmsrc'

ext {
    downloadUrl = "https://github.com/wordnik/swagger-ui/archive/v${swaggerUiVersion}.zip"
    destinationZip = "$buildDir/zip/${swaggerUiVersion}.zip"
    replacePath = 'window.location.origin + "\\${pageContext.request.contextPath}/api-docs"'
    artifactRepoBase = 'http://oss.jfrog.org/artifactory'
    artifactLabel = 'swagger-spring-mvc-ui'
}

task swaggerUiDownload << {
    if (!file(destinationZip)?.exists()) {
        download {
            src downloadUrl
            dest destinationZip
            onlyIfNewer true
        }
    }
}

task unzip(type: Copy) {
    from zipTree(file(destinationZip))
    include "*/dist/**"
    into file("${buildDir}")
}

/**
 * Creates the jsp from index.html
 */
task sdoc(type: Copy) {
    from("${buildDir}/${swaggerUiExplodedDir}/index.html")
    into("${buildDir}/${swaggerUiExplodedDir}")

    filter { String line ->
        line.replaceAll("\"http://petstore.swagger.wordnik.com/api/api-docs\"", replacePath)
                .replaceAll('/\\*', '')
                .replaceAll('\\*/', '')
    }

    rename('index.html', 'sdoc.jsp')
}

task removeHtmlIndex(type: Delete) {
    delete "${buildDir}/${swaggerUiExplodedDir}/index.html"
}


jar {
    from("${buildDir}/${swaggerUiExplodedDir}") {
        into "META-INF/resources"

        manifest {
            attributes(
//                    "Implementation-Title": artifactLabel,
//                    "Implementation-Version": version,
                    "SwaggerUi-Version": swaggerUiVersion,
                    "Swagger-Spring-MVC-Version": swaggerSpringMvcVersion
            )
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task cleanup(type: Delete) {
    delete "${buildDir}/${swaggerUiExplodedDir}"
}

clean.dependsOn(cleanup)
compileJava.dependsOn(removeHtmlIndex)
removeHtmlIndex.dependsOn(sdoc)
sdoc.dependsOn(unzip)
unzip.dependsOn(swaggerUiDownload)


repositories {
    jcenter()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    compile "io.springfox:springfox-core:2.0.0-SNAPSHOT"
    compile "io.springfox:springfox-spi:2.0.0-SNAPSHOT"
    compile "io.springfox:springfox-schema:2.0.0-SNAPSHOT"
    compile "io.springfox:springfox-spring-web:2.0.0-SNAPSHOT"
    compile "io.springfox:springfox-swagger2:2.0.0-SNAPSHOT"
    compile "org.ajar:swagger-spring-mvc-ui:0.4"

    compile project(':services-api')
}